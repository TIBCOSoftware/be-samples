/**
 * @description 
 */
void rulefunction RuleFunctions.GetGridMedata {
	attribute {
		validity = ACTION;
	}
	scope {
		Events.GetMetadata getMetadata;
	}
	body {
		if (GlobalProperties.STORE_TYPE == "Ignite") {
			System.debugOut("**** Grid Metadata functions are not supported with Ignite Store.");
			Event.replyEvent(getMetadata, getMetadata);
			return;
		}
		
		Object stringBuff = String.createBuffer(10);
		
		if (GlobalProperties.STORE_TYPE == "Cassandra") {
			String.append(stringBuff, "Version - " + Store.Metadata.getVersion(GlobalProperties.STORE_URL) + "\n");
		}
		
		String[] containerNames = Store.Metadata.getContainerNames(GlobalProperties.STORE_URL);
		for (int i=0; i<containerNames@length; i++) {
			String.append(stringBuff, "Container Name - " + containerNames[i] + "\n");
			
			String[] fieldNames = Store.Metadata.getContainerFieldNames(GlobalProperties.STORE_URL, containerNames[i]);
			for (int j=0; j<fieldNames@length; j++) {
				String.append(stringBuff, "\tField Name - " + fieldNames[j] + ", Type - " + Store.Metadata.getContainerFieldType(GlobalProperties.STORE_URL, containerNames[i], fieldNames[j]) + "\n");
			}
			
			String.append(stringBuff, "\tPrimary Index - " + Store.Metadata.getContainerPrimaryIndex(GlobalProperties.STORE_URL, containerNames[i]) + "\n");
			
			String[] indexNames = Store.Metadata.getContainerIndexNames(GlobalProperties.STORE_URL, containerNames[i]);
			for (int j=0; j<indexNames@length; j++) {
				String.append(stringBuff, "\tIndex Name- " + indexNames[j] + "\n");
				
				String[] indexFieldNames = Store.Metadata.getContainerIndexFieldNames(GlobalProperties.STORE_URL, containerNames[i], indexNames[j]);
				for (int k=0; k<indexFieldNames@length; k++) {
					String.append(stringBuff, "\t\tField Name - " + indexFieldNames[k] + "\n");
				}
			}
		}
		
		System.debugOut("Store Metadata Details .... \n" + String.convertBufferToString(stringBuff) + "\n");
		String.clearBuffer(stringBuff);
		
		Event.replyEvent(getMetadata, getMetadata);
	}
}